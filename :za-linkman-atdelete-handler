#!/usr/bin/env zsh

emulate -RL zsh
setopt extendedglob noshortloops typesetsilent warncreateglobal

if [[ "$1" = plugin ]]; then
    local type="$1" user="$2" plugin="$3" id_as="$4" dir="${5#%}" hook="$6"
else
    local type="$1" url="$2" id_as="$3" dir="${4#%}" hook="$5"
fi

if (( ${+ICE[lman]} )); then
    if [[ ! -d ${ZPFX}/man/man1 ]]; then
        +zi-log "{w} {b}linkman annex{rst}: Missing {url}${ZPFX}/man/man1{rst}, not deleting man page(s)" && return
    fi
    local manfiles=${dir}/**/*(.[1-9]|.[1-9].gz)(N.)
    declare -a manfile installed attempted failed
    for manfile in $~manfiles; do
        (( ${attempted[(I)$manfile:t]} )) && continue
        attempted+=($manfile:t)
        {
            test -f $manfile
            } && {
            command rm -f -- "${ZPFX}/man/man${${${manfile:t}//[!1-9]}[-1]}/$manfile:t" && installed+=("{obj}$manfile:t")
            } || {
            failed+=("{obj}$manfile:t")
        }
    done

    if (( !OPTS[opt_-q,--quiet] )); then
        if (( ${#installed} )); then
            +zi-log "{m} {b}linkman annex{rst}: Removed man page${${${#installed}/1/}:+s}: ${(j:, :)installed}"

        fi
        if (( ${#failed} )); then
            +zi-log "{e} {b}linkman annex{rst}: Failed to remove man page${${${#failed}/1/}:+s}: ${(j:, :)failed}"
        fi
    fi
fi
