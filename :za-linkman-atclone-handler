#!/usr/bin/env zsh

emulate -RL zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops
if [[ "$1" = plugin ]]
then
    local type="$1" user="$2" plugin="$3" id_as="$4" dir="${5#%}" hook="$6"
else
    local type="$1" url="$2" id_as="$3" dir="${4#%}" hook="$5"
fi
if (( ${+ICE[lman]} ))
then
    [[ -d ${ZPFX}/man/man1 ]] || {
        +zinit-message "{pre}linkman annex: {warn}Warning: {msg}Missing {url}${ZPFX}/man/man1{msg}, not installing man pages{â€¦}{rst}" && return
    }
    local manfiles=${dir}/**/*(.[1-9]|.[1-9].gz)(N.)
    declare -a manfile installed attempted failed
    for manfile in $~manfiles
    do
        (( ${attempted[(I)$manfile:t]} )) && continue
        attempted+=($manfile:t)
        {
            test -f $manfile
        } && {
            command ln -f -- $manfile "${ZPFX}/man/man${${${manfile:t}//[!1-9]}[-1]}" && installed+=("{obj}$manfile:t")
        } || {
            failed+=("{obj}$manfile:t")
        }
    done
    if (( !OPTS[opt_-q,--quiet] ))
    then
        if (( ${#installed} ))
        then
            +zinit-message "{pre}linkman annex: {msg}Installed man page${${${#installed}/1/}:+s}: ${(j:, :)installed}{rst}"
        fi
        if (( ${#failed} ))
        then
            +zinit-message "{pre}linkman annex: {error}Could not install man page${${${#failed}/1/}:+s}: ${(j:, :)failed}{rst}"
        fi
    fi
fi
