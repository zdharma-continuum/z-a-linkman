#!/usr/bin/env zsh

emulate -RL zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops

if [[ "$1" = plugin ]]; then
    local type="$1" user="$2" plugin="$3" id_as="$4" dir="${5#%}" hook="$6"
else
    local type="$1" url="$2" id_as="$3" dir="${4#%}" hook="$5"
fi

if (( ${+ICE[lman]} )); then
    [[ -d ${ZPFX}/man/man1 ]] || {
        +zinit-message "{w} linkman annex: Missing {url}${ZPFX}/man/man1{rst}, not installing man pages" && return
    }
    local manfiles=${dir}/**/*(.[1-9]|.[1-9].gz)(N.)
    declare -a manfile installed attempted failed
    for manfile in $~manfiles; do
        (( ${attempted[(I)$manfile:t]} )) && continue
        attempted+=($manfile:t)
        {
            test -f $manfile
        } && {
            command ln -f -- $manfile "${ZPFX}/man/man${${${manfile:t}//[!1-9]}[-1]}" && installed+=("{obj}$manfile:t")
        } || {
            failed+=("{obj}$manfile:t")
        }
    done

    if (( !OPTS[opt_-q,--quiet] )); then
        if (( ${#installed} )); then
            +zinit-message "{m} linkman annex: Installed man page${${${#installed}/1/}:+s}: ${(j:, :)installed}"
        fi
        if (( ${#failed} )); then
            +zinit-message "{e} linkman annex: Failed to install man page${${${#failed}/1/}:+s}: ${(j:, :)failed}"
        fi
    fi
fi
